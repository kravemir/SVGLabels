import groovy.swing.SwingBuilder

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':tool:publishMavenPublicationToSonatypeSnapshotsRepository')) {
        String password = null

        if (System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter password',
                        alwaysOnTop: true,
                        resizable: false,
                        locationRelativeTo: null,
                        pack: true,
                        show: true
                ) {
                    vbox {
                        label(text: "Please enter key passphrase:")
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            password = new String(input.password)
                            dispose();
                        })
                    }
                }
            }
        } else {
            def passwordRaw = System.console().readPassword("\nPlease enter key passphrase: ")
            password = new String(passwordRaw)
        }

        rootProject.subprojects { project ->
            signing {
                useGpgCmd()
            }

            project.publishing.repositories.each { repo ->
                repo.credentials.password = password
            }
        }
    }
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    group   'org.kravemir.svg.labels'
    version '0.3.0-SNAPSHOT'

    dependencies {
        compileOnly         "com.google.auto.value:auto-value-annotations:1.6"
        annotationProcessor "com.google.auto.value:auto-value:1.6"
    }

    ext {
        sonatypeUsername = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
    }

    repositories {
        mavenCentral()
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Jar) {
        from(rootProject.projectDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
        required { gradle.taskGraph.hasTask("publishMavenPublicationToSonatypeSnapshotsRepository") }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId subproject.group
                artifactId subproject.artifact
                version project.version

                from components.java
                artifact sourceJar
                artifact javadocJar

                pom.withXml {
                    asNode().with {
                        appendNode('name', "${subproject.group}:${subproject.artifact}")
                        appendNode('description', 'Java SVG Labels generator')
                        appendNode('url', 'https://github.com/kravemir/svg-labels')

                        appendNode('licenses').with {
                            appendNode('license').with {
                                appendNode('name', 'Apache License, Version 2.0')
                                appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
                            }
                        }

                        appendNode('developers').with {
                            appendNode('developer').with {
                                appendNode('name', 'Miroslav Kravec')
                                appendNode('email', 'kravec.miroslav@gmail.com')
                                appendNode('organization', null)
                            }
                        }

                        appendNode('scm').with {
                            appendNode('connection', 'scm:git:git://github.com/kravemir/svg-labels.git')
                            appendNode('developerConnection', 'scm:git:ssh://github.com:kravemir/svg-labels.git')
                            appendNode('url', 'https://github.com/kravemir/svg-labels/tree/master')
                        }
                    }
                }

                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }

                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc|executable)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
        repositories {
            maven {
                name "sonatypeSnapshots"
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username project.sonatypeUsername
                    password "-- not specified --"
                }
            }
        }
    }

    model {
        tasks.generatePomFileForMavenPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenPublicationToSonatypeSnapshotsRepository {
            dependsOn project.tasks.signArchives
        }
    }
}
