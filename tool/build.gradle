dependencies {
    compile project(':library')
    compile group: 'info.picocli', name: 'picocli', version: '3.1.0'

    testCompile project(path: ':library', configuration: 'testlibCompile')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

ext {
    artifact = "svg-labels-tool"
}

task createProperties(dependsOn: processResources) {
    doLast {
        File versionPropertiesFile = new File("$buildDir/resources/main/version.properties")
        versionPropertiesFile.getParentFile().mkdirs()
        versionPropertiesFile.withWriter { w ->
            Properties p = new Properties()
            p['org.kravemir.svg.labels.tool.version'] = project.version.toString()
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task executableJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Main-Class': 'org.kravemir.svg.labels.tool.ToolRunner'
    }
    classifier 'executable'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives executableJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact executableJar
        }
    }
}
