dependencies {
    compile project(':library')
    compile group: 'info.picocli', name: 'picocli', version: '2.3.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task createProperties(dependsOn: processResources) {
    doLast {
        File versionPropertiesFile = new File("$buildDir/resources/main/version.properties")
        versionPropertiesFile.getParentFile().mkdirs()
        versionPropertiesFile.withWriter { w ->
            Properties p = new Properties()
            p['org.kravemir.svg.labels.tool.version'] = project.version.toString()
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task fatJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Main-Class': 'org.kravemir.svg.labels.tool.ToolRunner'
    }
    classifier 'fat-jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId "svg-labels-tool"
            version project.version

            from components.java
            artifact fatJar
        }
    }
}
